# Maintainer: lsfxz
pkgname=bettercap-ng
pkgver=20180205.310_0f8c88e
pkgrel=1
pkgdesc="bettercap-ng"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libpcap')
makedepends=('go' 'git')
source=("bettercap-ng::git://github.com/evilsocket/bettercap-ng#branch=${BRANCH:-master}")
md5sums=('SKIP')

pkgver() {
	if [[ "$PKGVER" ]]; then
		echo "$PKGVER"
		return
	fi

	cd "$srcdir/$pkgname"
	local date=$(git log -1 --format="%cd" --date=short | sed s/-//g)
	local count=$(git rev-list --count HEAD)
	local commit=$(git rev-parse --short HEAD)
	echo "$date.${count}_$commit"
}

build() {
	cd "$srcdir/$pkgname"

	if [ -L "$srcdir/$pkgname" ]; then
		rm "$srcdir/$pkgname" -rf
		mv "$srcdir/.go/src/$pkgname/" "$srcdir/$pkgname"
	fi

	rm -rf "$srcdir/.go/src"

	mkdir -p "$srcdir/.go/src"

	export GOPATH="$srcdir/.go"

	mv "$srcdir/$pkgname" "$srcdir/.go/src/"

	cd "$srcdir/.go/src/$pkgname/"
	ln -sf "$srcdir/.go/src/$pkgname/" "$srcdir/$pkgname"

	git submodule update --init

	go get -v \
		-gcflags "-trimpath $GOPATH/src"
}

package() {
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/bettercap-ng"

  find "$srcdir/.go/bin/" -type f -executable | while read filename; do
	  install -DT "$filename" "$pkgdir/usr/share/bettercap-ng/$(basename $filename)"
  done

  cat > "$pkgdir/usr/bin/bettercap-ng" << EOF
cd /usr/share/bettercap-ng
exec ./bettercap-ng "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/bettercap-ng"

  cp -r --no-preserve=ownership "$srcdir/bettercap-ng/caplets" "$pkgdir/usr/share/bettercap-ng/caplets"
}
