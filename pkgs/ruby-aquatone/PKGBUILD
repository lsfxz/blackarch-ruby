# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# See COPYING for license details.

pkgname='ruby-aquatone'
pkgver=63.8d3496b
_gemver='0.5.0'
_gemname='aquatone'
pkgrel=3
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='a set of tools for performing reconnaissance on domain names.'
arch=('any')
url='https://github.com/michenriksen/aquatone'
license=('MIT')
depends=('ruby' 'ruby-childprocess-0.7' 'ruby-httparty-0.14')
makedepends=('ruby' 'git')
options=('!emptydirs' '!strip')
source=('git+https://github.com/michenriksen/aquatone.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/aquatone"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/aquatone"
  local _gemdir="$(ruby -e 'puts Gem.default_dir')"
  gem build ./aquatone.gemspec

  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$_gemver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$_gemver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$_gemver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$_gemver"/ && cp -r --parents lib exe $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$_gemver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$_gemver"

}
