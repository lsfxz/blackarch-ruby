# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# See COPYING for license details.

pkgname='ruby-global-hotkeys-manager'
pkgver=18.132f031
_gemname='global_hotkeys_manager'
_gitname='hotkeys-manager'
_gemver='0.1.3'
pkgrel=1
pkgdesc="Hotkeys for konsole. Don't use this."
arch=('any')
url='https://github.com/lsfxz/hotkeys-manager'
license=("MIT")
depends=('ruby' 'ruby-sinatra' 'ruby-sinatra-contrib')
makedepends=('git')
source=('git+https://github.com/lsfxz/hotkeys-manager.git')
options=('!emptydirs')
sha1sums=('SKIP' )

pkgver() {
  cd "$srcdir/$_gitname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_gitname"

  gem build $_gemname.gemspec
}

package() {
  cd "$srcdir/$_gitname"
  local _gemdir="$(ruby -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$_gemver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$_gemver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$_gemver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  install -D -m644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$_gemver"/ && cp -r --parents lib exe $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$_gemver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$_gemver"

}
