# See COPYING for license details.

pkgname='responder'
pkgver=304.c6bc263
pkgrel=2
epoch=2
pkgdesc='A LLMNR and NBT-NS poisoner, with built-in HTTP/SMB/MSSQL/FTP/LDAP rogue authentication server supporting NTLMv1/NTLMv2/LMv2, Extended Security NTLMSSP and Basic HTTP authentication.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-fuzzer' 'blackarch-spoof'
        'blackarch-networking')
arch=('any')
depends=('python2')
makedepends=('git')
url='https://github.com/lgandx/Responder/'
license=('GPL3')
source=('git+https://github.com/lgandx/Responder.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Responder"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Responder"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/responder"
  mkdir -p "$pkgdir/usr/share/doc/responder"

  install -Dm644 -t "$pkgdir/usr/share/doc/responder" README.md

  rm README.md

  grep -iRl 'python' . | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'

  cp -a * "$pkgdir/usr/share/responder"

  cat > "$pkgdir/usr/bin/responder" << EOF
#!/bin/sh
exec python2 /usr/share/responder/Responder.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/responder"

  cat > "$pkgdir/usr/bin/dhcp-responder" << EOF
#!/bin/sh
exec python2  /usr/share/responder/tools/DHCP.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/dhcp-responder"
}
