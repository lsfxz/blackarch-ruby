# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# See COPYING for license details.

pkgname='ruby-reek'
_gemname='reek'
pkgver='4.7.3'
pkgrel=5
pkgdesc="Code smell detector for Ruby"
arch=('any')
url='https://github.com/troessner/reek/wiki'
license=("MIT")
depends=('ruby' 'ruby-codeclimate-engine-rb' 'ruby-parser' 'ruby-rainbow-2')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('8e6351513b4a6f804f0cc9fb5847909c6ab9c1bc' )


package() {
  local _gemdir="$(ruby -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/License.txt" "$pkgdir/usr/share/licenses/$pkgname/License.txt"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib bin $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
